-------------------------------------------------------------------

+(<+++++[>++++++++++<-]>-->)
>+++++[<++++++++++>-]<--(>) 		
>++++++++[<+(>)>-] 			
<+(----------<+:) 			

>>>>>>>>>>>>>> 				
+[,----------[>+<-]++>]<--< 		

[>]+< 					
[<]<<< << ++++[>++++++++++<-]>++++++++ 	

[					
	>>+[>[<-]<[->+<]>]> 		
	-[				
		[-]< +[<[>-]>[-<+>]<]< 	
		+: [>]			
	]<		
]

+[<[>-]>[-<+>]<]< 			

[<]>[.>] 	

-------------------------------------------------------------------

+(<+++++[>++++++++++<-]>-->) 		//Процедура под номером 1; прибавляет к текущей ячейке 48 (превращает ее в 49, код символа 1)
>+++++[<++++++++++>-]<--(>) 		//Процедура под номером 49 (код символа 1); сдвигает указатель вправо
>++++++++[<+(>)>-] 			//Процедуры под номером 50-57 (коды символов 2-9); сдвигают указатель вправо
<+(----------<+:) 			//Процедура под номером 58 (переполнение разряда); отнимает от текущей ячейки 10 (превращает ее в 48, код символа 0), сдвигает указатель влево, прибавляет к текущей ячейке 1 и вызывает соотвествующую процедуру


>>>>>>>>>>>>>> 				//Сдвигает указатель на 15 разрядов вправо, освобождая место слева от каретки для нормальной работы машины Тьюринга
+[,----------[>+<-]++>]<--< 		//Пока не введен перенос строки, считывает введенные символы, заменяет их числовые значения на 2 (играет роль v)

[>]+< 					//Реализация нулевого состояния: пока не встречен пробел, каретка движется вправо, первый встреченный пробел заменяется значением 1 (играет роль *)
[<]<<< << ++++[>++++++++++<-]>++++++++ 	//Реализация первого состояния: пока не встречен пробел, каретка движется влево; после нахождения пробела каретка смещается еще на 2 ячейки влево, и там пробел заменяется значением 48 (код символа 0)

[					//Реализация второго, третьего, четвертого состояния в совокупности:
	>>+[>[<-]<[->+<]>]> 		//Второе состояние: пока не найден непробельный символ, каретка движется вправо
	-[				//От найденного символа отнимается 1: если после этого ячейка ненулевая, значит, это была метка v, а не знак окончания последовательности *
		[-]< +[<[>-]>[-<+>]<]< 	//Тогда метка удаляется, пока не найден первый непробельный символ, каретка движется влево
		+: [>]			//К найденной ненулевой ячейке - цифре - прибавляется 1 и вызывается соотвествующая процедура: либо смещения вправо один раз, либо обработки переполнения разрядов столько, сколько потребуется, тоже со смещением вправо в конце. После этого указатель устанавливается на последнюю цифру числа.
	]<		
]

+[<[>-]>[-<+>]<]< 			//Иначе была найдена единица, аналог *, и запускается реализация пятого состояния: каретка движется влево, пока не найден непробельный символ, и останавливается на последней цифре полученного числа

[<]>[.>] 				//Вывод полученного числа

